!: BOLT #VERSION#
!: ALLOW CONCURRENT

{{
    A: HELLO {"user_agent": "*", "[routing]": "*", "scheme": "basic", "principal": "neo4j", "credentials": "pass"}
    *: RESET

    {{
        # connection 1 for session 1 will live through the entire test
        C: BEGIN {"{}": "*"}
        S: SUCCESS {}

        C: RUN "RETURN 1/2 AS n" {} {}
        S: SUCCESS {"fields": ["n"]}
        C: PULL {"n": {"Z": "*"}}
        S: RECORD [1]
           SUCCESS {"type": "r"}

        C: RUN "RETURN 2/2 AS n" {} {}
        S: SUCCESS {"fields": ["n"]}
        C: PULL {"n": {"Z": "*"}}
        S: RECORD [1]
           SUCCESS {"type": "r"}

        C: COMMIT
        S: SUCCESS {}
    ----
        # connection 2 for session 2 will will be killed to make room for throwaway connection 3
        C: RUN "RETURN 2 AS n" {"{}": "*"} {"{}": "*"}
        S: SUCCESS {"fields": ["n"]}
        C: PULL {"n": {"Z": "*"}}
        S: RECORD [1]
           SUCCESS {"type": "r"}

        *: RESET
        A: GOODBYE
    }}
----
    # connection 3 for session 3 with session auth
    A: HELLO {"user_agent": "*", "[routing]": "*", "scheme": "basic", "principal": "neo5j", "credentials": "pass++"}
    *: RESET

    C: RUN "RETURN 3 AS n" {"{}": "*"} {"{}": "*"}
    S: SUCCESS {"fields": ["n"]}
    C: PULL {"n": {"Z": "*"}}
    S: RECORD [1]
       SUCCESS {"type": "r"}

    *: RESET
    A: GOODBYE
}}

*: RESET
?: GOODBYE
