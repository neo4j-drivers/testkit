%ignore WS
%ignore COMMENT

start : [NLS] (bang_line NLS)* block_list [NLS]

block_list : block (NLS block)*

?block : client_block
       | server_block
       | alternative_block
       | parallel_block
       | optional_block
       | repeat_0_block
       | repeat_1_block

alternative_block : ALT_START_L NLS (block_list NLS ALT_SEP_L NLS)+ block_list NLS ALT_END_L
parallel_block : PAR_START_L NLS (block_list NLS PAR_SEP_L NLS)+ block_list NLS PAR_END_L
optional_block : OPT_START_L NLS block_list NLS OPT_END_L
repeat_0_block : LOOP0_START_L NLS block_list NLS LOOP0_END_L
repeat_1_block : LOOP1_START_L NLS block_list NLS LOOP1_END_L

ALT_START_L : "{{"
ALT_SEP_L : "----"
ALT_END_L : "}}"
PAR_START_L : "{{"
PAR_SEP_L : "++++"
PAR_END_L : "}}"
OPT_START_L : "{?"
OPT_END_L : "?}"
LOOP0_START_L : "{*"
LOOP0_END_L : "*}"
LOOP1_START_L : "{+"
LOOP1_END_L : "+}"

bang_line : BANG_MARKER WS* LINE
client_block : explicit_client_line (NLS implicit_client_line)*
explicit_client_line : CLIENT_MARKER WS* LINE  -> client_line
implicit_client_line.-1: LINE                  -> client_line
server_block : explicit_server_line (NLS implicit_server_line)*
explicit_server_line : SERVER_MARKER WS* LINE  -> server_line
implicit_server_line.-1: LINE                  -> server_line

WS : /[ \t\f\r]/+
COMMENT: /^/m WS* /#[^\n]*/m [NLS]
// this is ugly but needed to avoid unprocessable amounts of ambiguity in the grammar
LINE : /(?!\s*(!:|S:|C:|#|{{|----|\+\+\+\+|}}|{[?*+]|[?*+]\}))[^\n]+/

BANG_MARKER : "!:"
CLIENT_MARKER : "C:"
SERVER_MARKER : "S:"

CR : /\r/
LF : /\n/
NL: (CR? LF)
NLS: NL ([WS] NL)*
